{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "98a0897f",
   "metadata": {
    "tags": [
     "remove-cell"
    ]
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "itproot"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "include(\"../src.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4c5407f2",
   "metadata": {},
   "source": [
    "# ITP метод\n",
    "\n",
    "В общем виде алгоритм с локализацией корня (бисекция, regula falsi, метод Риддерса) можно описать так.\n",
    "\n",
    "(algo:bracketing)=\n",
    "```{proof:algorithm} Алгоритм с локализацией корня\n",
    "Входные данные: функция $f$; начальный интервал $a$, $b$, такой что $f(a) f(b) < 0$; $\\text{xtol}$.\n",
    "\n",
    "1. Выбрать некоторое $\\tilde{x} \\in (a, b)$.\n",
    "2. По $f(\\tilde{x})$ обновить интервал $(a, b)$ так, чтобы функция имела разные знаки на концах нового интервала.\n",
    "3. Если достигнута точность $|b - a| < \\text{xtol}$, завершить, иначе -- перейти на шаг 1.\n",
    "```\n",
    "\n",
    "Метод бисекции обладает замечательным свойством -- он **заранее** предсказывает необходимое число итераций $n_{1/2}$ по заданной точности локализации корня $\\text{xtol}$. \n",
    "\n",
    "Авторы работы {cite}`itp2020` показали, что это свойство (называемое minmax optimality) сохраняется во всяком алгоритме с локализацией корня, если новое приближение $\\tilde{x}$ попадает в область $I = [x_{1/2}-r, x_{1/2}+r]$ вокруг середины интервала $x_{1/2}$. Ниже приведено выражение для $r$ на $i$-ой итерации алгоритма\n",
    "\n",
    "```{math}\n",
    ":label: itp_radius\n",
    "\n",
    "r_i = \\text{xtol } 2^{n_{1/2} - i} - \\frac{b_i - a_i}{2}, \\quad i = 1, 2, ..., n_{1/2}.\n",
    "```\n",
    "\n",
    "Так, можно модифицировать {numref}`Алгоритм %s <algo:bracketing>`, добавив после шага 1 проверку: попадает ли приближение $\\tilde{x}$ в область $I$. Если попадает, то оставить его, как есть, а если не попадает, взять приближение как у бисекции $\\tilde{x} := x_{1/2}$. Таким образом, модифицированный алгоритм гарантированно будет работать не хуже бисекции, а в лучшем случае как алгоритм, взятый за основу (например, метод Риддерса).\n",
    "\n",
    "## Описание метода\n",
    "\n",
    "Самими же авторами был предложен метод ITP (Interpolate Truncate and Project) {cite}`itp2020`.\n",
    "\n",
    "**Interpolate**. На этом шаге определяется $x_f$ приближение по методу regula falsi (пересечение абсциссы секущей).\n",
    "\n",
    "**Truncate**. В этом методе, помимо описанной выше модификации присутствует шаг truncate, смещающий найденный при интерполяции корень в сторону $x_{1/2}$. Делается это в надежде на уменьшение интервала $[a, b]$ более чем в два раза. Результат этого шага точка $x_t$.\n",
    "\n",
    "**Project**. На этом шаге определяется, попадает ли $x_t$ в область minmax optimality. Если $x_t$ не попадает, то он перемещается (проецируется) в эту область и получается $x_{\\text{ITP}}$. В конце итерации происходит выбор нового отрезка по точкам $a_i$, $b_i$ и $x_{\\text{ITP}}$.\n",
    "\n",
    "В методе присутствует три параметра. Параметр $n_0 \\ge 0$ определяет число дополнительных итераций, в результате в формуле {eq}`itp_radius` присутствует не $n_{1/2}$, а $n_\\max = n_{1/2} + n_0$. Параметры $\\kappa_1 > 0$ и $\\kappa_2 \\in [1, 1 + \\phi)$ ($\\phi \\approx 1.618$, золотое сечение) определяют, как происходит truncate шаг. Так, они задают область $[x_{1/2} - \\delta, x_{1/2}+\\delta]$, где \n",
    "\n",
    "```{math}\n",
    "\\delta = \\kappa_1 (b_i - a_i)^{\\kappa_2} / (b_0 - a_0).\n",
    "```\n",
    "\n",
    "Если $x_f$ находится за пределами области $\\delta \\le |x_{1/2} - x_f|$, то он смещается в сторону середины $x_{1/2}$ на $\\delta$. Иначе устанавливается в $x_t := x_{1/2}$.\n",
    "\n",
    "Авторами метода показано, что в случае дважды дифференциируемой функции метод сходится к простому корню со скоростью $\\sqrt{\\kappa_2}$. Например, в случае $\\kappa_2 = 2$ ожидается сверхлинейная сходимость со скоростью $\\sqrt{2}$.\n",
    "\n",
    "## Реализация\n",
    "\n",
    "```{proof:function} itproot\n",
    "\n",
    "**Метод ITP**\n",
    "\n",
    ":::julia\n",
    "\"\"\"\n",
    "Метод ITP поиска корня `f`(x) = 0 c точностью `xtol`.\n",
    "\"\"\"\n",
    "function itproot(f, x₁, x₂; xtol=eps(), ftol=eps(), κ₁=0.1, κ₂=2, n₀=1)\n",
    "    if x₁ > x₂; x₁, x₂ = x₂, x₁; end\n",
    "    y₁, y₂ = f(x₁), f(x₂)\n",
    "    y₁ * y₂ > 0 && error(\"Функция должна иметь разные знаки в концах отрезка\")\n",
    "    y₁ == 0 && return x₁\n",
    "    y₂ == 0 && return x₂\n",
    "    \n",
    "    nbisect = ceil(Int, log2((x₂-x₁)/xtol))\n",
    "    maxiter = nbisect + n₀\n",
    "    brackorig = x₂ - x₁\n",
    "\n",
    "    for i in 1:maxiter\n",
    "        # interpolate\n",
    "        xf = (y₂*x₁ - y₁*x₂)/(y₂ - y₁)\n",
    "\n",
    "        # truncate\n",
    "        xmid = (x₁ + x₂)/2\n",
    "        σ = sign(xmid - xf)\n",
    "        δ = κ₁ * (x₂ - x₁)^κ₂ / brackorig\n",
    "        xt = δ ≤ abs(xmid - xf) ? xf + copysign(δ, σ) : xmid\n",
    "        \n",
    "        # project\n",
    "        r = xtol * 2.0^(maxiter - i) - (x₂ - x₁)/2\n",
    "        xnew = abs(xt - xmid) ≤ r ? xt : xmid - copysign(r, σ)\n",
    "\n",
    "        ynew = f(xnew)\n",
    "        if sign(y₂) == sign(ynew)\n",
    "            x₂, y₂ = xnew, ynew\n",
    "        elseif sign(y₁) == sign(ynew)\n",
    "            x₁, y₁ = xnew, ynew\n",
    "        else  # ynew == 0\n",
    "            return xnew\n",
    "        end\n",
    "        if abs(ynew) < ftol || abs(x₂ - x₁) < xtol\n",
    "            return (x₁ + x₂)/2\n",
    "        end\n",
    "    end\n",
    "    return (x₁ + x₂)/2\n",
    "end\n",
    ":::\n",
    "```\n",
    "\n",
    "```{proof:demo} Метод ITP\n",
    "```\n",
    "```{raw} html\n",
    "<div class=\"demo\">\n",
    "```\n",
    "\n",
    "Рассмотрим работу метода на функции $xe^x - 1$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "67fc4d86",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "itproot(f, -1, 1; xtol = 2.0e-10, n₀ = 0) = 0.5671432904087239\n",
      "ridders(f, -1, 1; xtol = 2.0e-10, maxiter = 4) = 0.5671432904097838\n"
     ]
    }
   ],
   "source": [
    "f = (x) -> x*exp(x) - 1\n",
    "@show itproot(f, -1, 1; xtol=2e-10, n₀=0)\n",
    "@show ridders(f, -1, 1; xtol=2e-10, maxiter=4);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9d0cffef",
   "metadata": {},
   "source": [
    "Если немного поменять функцию `itproot`, то можно увидеть, что метод справился с поиском корня за 8 итераций, тогда как бисекция требует 34. В то же время метод Риддерса справляется за 4 итерации, **но** он требует 8 вычислений функции (по два вычисления за итерацию).\n",
    "\n",
    "На функции $\\ln |x - 10/9|$ ITP выигрывает по числу вычислений функции."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "ad439636",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "itproot(f, -1, 1; xtol = 2.0e-10, n₀ = 0) = 0.11111111111111119\n",
      "ridders(f, -1, 1; xtol = 2.0e-10, maxiter = 5) = 0.1111111111111111\n"
     ]
    }
   ],
   "source": [
    "f = (x) -> (log ∘ abs)(x - 10/9)\n",
    "@show itproot(f, -1, 1; xtol=2e-10, n₀=0)\n",
    "@show ridders(f, -1, 1; xtol=2e-10, maxiter=5);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "48ba074a",
   "metadata": {},
   "source": [
    "Здесь ITP потребовалось 8 вычислений, бисекции 34, а методу Риддерса 10.\n",
    "\n",
    "Сравнение на большем числе функций вы можете посмотреть в оригинальной работе {cite}`itp2020`.\n",
    "\n",
    "```{raw} html\n",
    "</div>\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst"
   }
  },
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.2"
  },
  "source_map": [
   13,
   17,
   124,
   128,
   134,
   138
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}